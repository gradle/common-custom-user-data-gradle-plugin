plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'jvm-test-suite'
    id 'java-test-fixtures'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'com.github.breadmoirai.github-release' version '2.5.2'
    id 'org.gradle.wrapper-upgrade' version '0.11.4'
}

def releaseVersion = releaseVersion()
def releaseNotes = releaseNotes()

group = 'com.gradle'
version = releaseVersion.get()
description = 'A Gradle plugin to capture common custom user data used for Gradle Build Scans in Develocity'

repositories {
    gradlePluginPortal()
    maven {
        url = uri("https://repo.grdev.net/artifactory/public")
        content {
            includeVersion("com.gradle", "develocity-gradle-plugin", "3.17-rc-2")
        }
    }
}

sourceSets {
    create("compatibilityApi")
    create("develocityCompatibility")
    create("enterpriseCompatibility")
}

dependencies {
    compatibilityApiCompileOnly(gradleApi())

    develocityCompatibilityCompileOnly(gradleApi())
    develocityCompatibilityCompileOnly('com.gradle:develocity-gradle-plugin:3.17-rc-2')
    develocityCompatibilityImplementation(sourceSets.compatibilityApi.output)

    enterpriseCompatibilityCompileOnly(gradleApi())
    enterpriseCompatibilityCompileOnly('com.gradle:gradle-enterprise-gradle-plugin:3.16.2')
    enterpriseCompatibilityImplementation(sourceSets.compatibilityApi.output)

    api(sourceSets.compatibilityApi.output)
    implementation(sourceSets.develocityCompatibility.output)
    implementation(sourceSets.enterpriseCompatibility.output)

    testFixturesImplementation('org.mockito:mockito-core:4.11.0')
}

wrapperUpgrade {
    gradle {
        'common-custom-user-data-gradle-plugin' {
            repo = 'gradle/common-custom-user-data-gradle-plugin'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jar {
    from sourceSets.compatibilityApi.output
    from sourceSets.develocityCompatibility.output
    from sourceSets.enterpriseCompatibility.output
}

gradlePlugin {
    website = 'https://github.com/gradle/common-custom-user-data-gradle-plugin'
    vcsUrl = 'https://github.com/gradle/common-custom-user-data-gradle-plugin.git'

    automatedPublishing = true

    plugins {
        commonCustomUserData {
            id = 'com.gradle.common-custom-user-data-gradle-plugin'
            displayName = 'Develocity Common Custom User Data Gradle Plugin'
            description = releaseNotes.get()
            implementationClass = 'com.gradle.CommonCustomUserDataGradlePlugin'
            tags.addAll('android', 'java', 'develocity')
        }
    }
}

tasks.withType(ValidatePlugins).configureEach {
    failOnWarning = true
    enableStricterValidation = true
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation(platform('org.junit:junit-bom:5.10.2'))
                implementation('org.junit.jupiter:junit-jupiter')
            }
        }

        enterpriseCompatibilityTest(org.gradle.api.plugins.jvm.JvmTestSuite) {
            useJUnitJupiter()

            dependencies {
                implementation(project())
                implementation(sourceSets.enterpriseCompatibility.output)
                implementation(testFixtures(project()))
                implementation(platform('org.junit:junit-bom:5.10.2'))
                implementation('org.junit.jupiter:junit-jupiter')
                implementation('org.mockito:mockito-core:4.11.0')
                implementation('org.mockito:mockito-junit-jupiter:4.11.0')
                implementation('com.gradle:gradle-enterprise-gradle-plugin:3.16.2') {
                    because("Verify that reflective proxies and adapters work with actual plugin classes")
                }
            }
        }

        develocityCompatibilityTest(org.gradle.api.plugins.jvm.JvmTestSuite) {
            useJUnitJupiter()

            dependencies {
                implementation(project())
                implementation(sourceSets.develocityCompatibility.output)
                implementation(testFixtures(project()))
                implementation(platform('org.junit:junit-bom:5.10.2'))
                implementation('org.junit.jupiter:junit-jupiter')
                implementation('org.mockito:mockito-core:4.11.0')
                implementation('org.mockito:mockito-junit-jupiter:4.11.0')
                implementation('com.gradle:develocity-gradle-plugin:3.17-rc-2') {
                    because("Verify that reflective proxies and adapters work with actual plugin classes")
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.enterpriseCompatibilityTest)
    dependsOn(testing.suites.develocityCompatibilityTest)
}

/*
The rest of the build logic in this file is only required for publishing to the Gradle Plugin Portal.
When using this project as a template for your own plugin to publish internally, you should delete all code following this comment.
You may also remove `plugin-publish` and `signing` from the `plugins {}` block above.
 */

signing {
    // Require publications to be signed on CI. Otherwise, publication will be signed only if keys are provided.
    required providers.environmentVariable('CI').isPresent()

    useInMemoryPgpKeys(
        providers.environmentVariable('PGP_SIGNING_KEY').orNull,
        providers.environmentVariable('PGP_SIGNING_KEY_PASSPHRASE').orNull
    )
}

githubRelease {
    token = System.getenv('CCUD_GIT_TOKEN') ?: ''
    owner = 'gradle'
    repo = 'common-custom-user-data-gradle-plugin'
    targetCommitish = 'main'
    releaseName = releaseVersion
    tagName = releaseVersion.map { "v$it" }
    prerelease = false
    overwrite = false
    generateReleaseNotes = false
    body = releaseNotes
}

def createReleaseTag = tasks.register('createReleaseTag', CreateGitTag) {
    // Ensure tag is created only after a successful publishing
    mustRunAfter('publishPlugins')
    tagName = githubRelease.tagName.map { it.toString() }
}

tasks.named('githubRelease').configure {
    dependsOn(createReleaseTag)
}

tasks.withType(com.gradle.publish.PublishTask) {
    notCompatibleWithConfigurationCache("$name task does not support configuration caching")
}

def releaseVersion() {
    def releaseVersionFile = layout.projectDirectory.file('release/version.txt')
    return providers.fileContents(releaseVersionFile).asText.map { it -> it.trim() }
}

def releaseNotes() {
    def releaseNotesFile = layout.projectDirectory.file('release/changes.md')
    return providers.fileContents(releaseNotesFile).asText.map { it -> it.trim() }
}
