plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'com.github.breadmoirai.github-release' version '2.4.1'
    id 'org.gradle.wrapper-upgrade' version '0.11.1'
}

group = 'com.gradle'
version = layout.projectDirectory.file('release/version.txt').asFile.text.trim()

repositories {
    gradlePluginPortal()
}

dependencies {
    compileOnly 'com.gradle:gradle-enterprise-gradle-plugin:3.12.6'
}

wrapperUpgrade {
    gradle {
        'common-custom-user-data-gradle-plugin' {
            repo = 'gradle/common-custom-user-data-gradle-plugin'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

gradlePlugin {
    website = 'https://github.com/gradle/common-custom-user-data-gradle-plugin'
    vcsUrl = 'https://github.com/gradle/common-custom-user-data-gradle-plugin.git'

    automatedPublishing = true

    def releaseNotes = layout.projectDirectory.file('release/changes.md').asFile.text
    plugins {
        commonCustomUserData {
            id = 'com.gradle.common-custom-user-data-gradle-plugin'
            displayName = 'Gradle Enterprise Common Custom User Data Gradle Plugin'
            description = """\
                          |A Gradle plugin to capture common custom user data used for Gradle Build Scans in Gradle Enterprise
                          |
                          |$releaseNotes
                          """.stripMargin().trim()
            implementationClass = 'com.gradle.CommonCustomUserDataGradlePlugin'
            tags.addAll('android', 'java', 'gradle enterprise')
        }
    }
}

tasks.withType(ValidatePlugins).configureEach {
    failOnWarning = true
    enableStricterValidation = true
}

/*
The rest of the build logic in this file is only required for publishing to the Gradle Plugin Portal.
When using this project as a template for your own plugin to publish internally, you should delete all code following this comment.
You may also remove `plugin-publish` and `signing` from the `plugins {}` block above.
 */

signing {
    // Require publications to be signed on CI. Otherwise, publication will be signed only if keys are provided.
    required providers.environmentVariable('CI').isPresent()

    useInMemoryPgpKeys(
        providers.environmentVariable('PGP_SIGNING_KEY').orNull,
        providers.environmentVariable('PGP_SIGNING_KEY_PASSPHRASE').orNull
    )
}

githubRelease {
   token = System.getenv('CCUD_GIT_TOKEN') ?: ''
   owner = 'gradle'
   repo = 'common-custom-user-data-gradle-plugin'
   targetCommitish = 'main'
   releaseName = gitHubReleaseName()
   tagName = gitReleaseTag()
   prerelease = false
   overwrite = false
   generateReleaseNotes = false
   body = layout.projectDirectory.file('release/changes.md').asFile.text.trim()
}

def createReleaseTag = tasks.register('createReleaseTag', CreateGitTag) {
    // Ensure tag is created only after a successful publishing
    mustRunAfter('publishPlugins')
    tagName = gitReleaseTag()
}

tasks.named('githubRelease').configure {
    dependsOn(createReleaseTag)
}

tasks.withType(Sign).configureEach {
    notCompatibleWithConfigurationCache("$name task does not support configuration caching")
}

def gitHubReleaseName() {
    return version.toString()
}

def gitReleaseTag() {
    return "v${version}"
}
