plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'com.gradle.plugin-publish' version '0.20.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

group = 'com.gradle'
version = layout.projectDirectory.file("release/version.txt").asFile.text.trim()

repositories {
    gradlePluginPortal()
}

dependencies {
    compileOnly 'com.gradle:gradle-enterprise-gradle-plugin:3.8.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

gradlePlugin {
    plugins {
        commonCustomUserData {
            id = 'com.gradle.common-custom-user-data-gradle-plugin'
            displayName = 'Gradle Enterprise Common Custom User Data Gradle Plugin'
            description = 'A Gradle plugin to capture common custom user data used for Gradle Build Scans in Gradle Enterprise'
            implementationClass = 'com.gradle.CommonCustomUserDataGradlePlugin'
        }
    }
}

tasks.withType(ValidatePlugins).configureEach {
    failOnWarning = true
    enableStricterValidation = true
}

/*
The rest of the build logic in this file is only required for publishing to the Gradle Plugin Portal.
When using this project as a template for your own plugin to publish internally, you should delete all code following this comment.
You may also remove `plugin-publish` and `signing` from the `plugins {}` block above.
 */
pluginBundle {
    website = "https://github.com/gradle/common-custom-user-data-gradle-plugin"
    vcsUrl = "https://github.com/gradle/common-custom-user-data-gradle-plugin.git"
    tags = ["gradle", "gradle enterprise"]
}

signing {
    // Require publications to be signed on CI. Otherwise, publication will be signed only if keys are provided.
    required providers.environmentVariable("CI").forUseAtConfigurationTime().isPresent()

    useInMemoryPgpKeys(
        providers.environmentVariable("PGP_SIGNING_KEY").forUseAtConfigurationTime().orNull,
        providers.environmentVariable("PGP_SIGNING_KEY_PASSPHRASE").forUseAtConfigurationTime().orNull)

    sign(configurations.archives)
}

githubRelease {
   token = System.getenv("CCUD_GIT_TOKEN") ?: ''
   owner = "gradle"
   repo = "common-custom-user-data-gradle-plugin"
   targetCommitish = "main"
   releaseName = version.toString()
   tagName = releaseTag()
   prerelease = false
   overwrite = false
   body = layout.projectDirectory.file("release/changes.md").asFile.text.trim()
}

tasks.register('createReleaseTag', CreateGitTag) {
    tagName = releaseTag()
}

tasks.named("githubRelease") {
    dependsOn("createReleaseTag")
}

def releaseTag() {
    return "v${version}"
}
